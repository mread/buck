/*
 * Copyright 2013-present Facebook, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License. You may obtain
 * a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

options {
  STATIC = false;
}

PARSER_BEGIN(XcconfigParser)

package com.facebook.buck.apple.xcode.xcconfig;

import com.google.common.base.Preconditions;
import com.google.common.base.Optional;
import com.google.common.base.Throwables;
import com.google.common.collect.ImmutableList;
import com.google.common.collect.ImmutableSortedSet;

import com.facebook.buck.util.ProjectFilesystem;

import java.io.IOException;
import java.nio.file.FileSystems;
import java.nio.file.InvalidPathException;
import java.nio.file.Path;
import java.io.Reader;
import java.io.FileReader;
import java.io.StringReader;

/** Parse Xcconfig files. */
public class XcconfigParser {

  private Optional<ProjectFilesystem> fileSystem;
  private Optional<Path> inputPathInProject;
  private ImmutableList<Path> additionalSearchPaths;

  private static XcconfigParser of(Reader input,
                                   Optional<ProjectFilesystem> fileSystem,
                                   Optional<Path> inputPathInProject,
                                   ImmutableList<Path> additionalSearchPaths) {
    XcconfigParser parser = new XcconfigParser(input);
    parser.fileSystem = Preconditions.checkNotNull(fileSystem);
    parser.inputPathInProject = Preconditions.checkNotNull(inputPathInProject);
    parser.additionalSearchPaths = Preconditions.checkNotNull(additionalSearchPaths);
    return parser;
  }

  public static ImmutableList<PredicatedConfigValue> parse(
      ProjectFilesystem fileSystem,
      Path inputPathInProject,
      ImmutableList<Path> searchPaths) throws ParseException {
    Optional<Reader> readerOpt = fileSystem.getReaderIfFileExists(inputPathInProject);
    if (!readerOpt.isPresent()) {
      throw new ParseException("Cannot open file: " + inputPathInProject);
    }
    try {
        return XcconfigParser.of(readerOpt.get(),
                                 Optional.<ProjectFilesystem>of(fileSystem),
                                 Optional.<Path>of(inputPathInProject),
                                 searchPaths)
                             .file();
    } catch (Throwable e) {  // javacc does not parse java7's multi-exception catch
      if (e instanceof ParseException || e instanceof TokenMgrError) {
        ParseException e1 = new ParseException("In file: " + inputPathInProject);
        e1.initCause(e);
        throw e1;
      } else {
        throw Throwables.propagate(e);
      }
    }
  }

  public static PredicatedConfigValue parseSetting(String setting) throws ParseException {
    return XcconfigParser.of(new StringReader(setting),
                             Optional.<ProjectFilesystem>absent(),
                             Optional.<Path>absent(),
                             ImmutableList.<Path>of())
                         .buildSetting();
  }

  private ImmutableList<PredicatedConfigValue> parseIncludedFile(String includeFileName)
      throws ParseException {

    if (!fileSystem.isPresent()) {
      throw new ParseException("Cannot include files without a filesystem");
    }

    // Parse the included file.
    Path includeFilePath = FileSystems.getDefault().getPath(includeFileName);
    Path includeFilePathInProject = null;

    if (includeFilePath.isAbsolute()) {
      // this feature is missing in xcodebuild : for testing purpose only!
      includeFilePathInProject = includeFilePath;
    } else if (inputPathInProject.isPresent()) {
      // check relative to current file
      try {
        includeFilePathInProject = inputPathInProject.get().resolveSibling(includeFilePath);
        if (!fileSystem.get().exists(includeFilePathInProject)) {
            includeFilePathInProject = null;
        }
      } catch (InvalidPathException e) {
        includeFilePathInProject = null;
      }
    }

    // additional search paths
    if (includeFilePathInProject == null) {
        for (Path searchPath : additionalSearchPaths) {
            Path path = searchPath.resolve(includeFilePath);
            if (fileSystem.get().exists(path)) {
                includeFilePathInProject = path;
                break;
            }
        }
    }

    if (includeFilePathInProject == null) {
      throw new ParseException("Cannot resolve path for included file: " + includeFileName);
    }

    return parse(fileSystem.get(), includeFilePathInProject, additionalSearchPaths);
  }

}

PARSER_END(XcconfigParser)

<DEFAULT> SKIP:
{
  " "
| "\n"
| "\r"
| "\t"
| < "//" (~["\n"])* "\n" >
}

<DEFAULT> TOKEN:
{
  < LITERAL: (["A"-"Z", "a"-"z", "0"-"9", "_"])+ >
| < LEFT_BRACKET: "[" > : CONDITION
| < EQUAL: "=" (" ")* > : SETTING_VALUE
| < INCLUDE: "#include" >
| < QUOTED_STRING: "\"" (~["\""])* "\"" >
}

<SETTING_VALUE> SKIP:
{
  < "//" (~["\n"])* >
}

<SETTING_VALUE> TOKEN:
{
  < DOLLAR_LEFT_PAREN: "$(" >
| < RIGHT_PAREN: ")" >
| < DOLLAR_LEFT_BRACE: "${" >
| < RIGHT_BRACE: "}" >
| < DOLLAR_LITERAL: "$" (["A"-"Z", "a"-"z", "0"-"9", "_"])+ >
| < VALUE_LITERAL : "/" // Note: if we get here the next char is not a slash, otherwise
                        // this would not be the longest possible match in this state
                  | (("/")? (~["\n", "$", "}", ")", "/"])+) >
| < EOL: "\n" > : DEFAULT
}

<CONDITION> TOKEN:
{
  < CONDITION_EQUAL: "=" >
| < CONDITION_IDENTIFIER: (["a"-"z", "0"-"9", "_", "."])+ >
| < STAR: "*" >
| < COMMA: "," >
| < RIGHT_BRACKET: "]" > : DEFAULT
}

ImmutableList<PredicatedConfigValue> file() :
{
  ImmutableList.Builder<PredicatedConfigValue> builder = ImmutableList.builder();
  Token file;
  PredicatedConfigValue setting;
}
{
  (
    ( <INCLUDE> file = <QUOTED_STRING>
      {
        String s = file.image;
        builder.addAll(parseIncludedFile(s.substring(1, s.length()-1)));
      }
    )
  | ( setting = buildSetting()
      {
        builder.add(setting);
      }
    )
  )*
  {
   return builder.build();
  }
}

PredicatedConfigValue buildSetting() :
{
  Token key;
  ImmutableSortedSet.Builder<Condition> conditions = ImmutableSortedSet.naturalOrder();
  ImmutableList<TokenValue> tokenValues;
}
{
  (
    ( key = <LITERAL> )
    ( <LEFT_BRACKET> ( parseCondition(conditions)
                     (<COMMA> parseCondition(conditions))* )?
      <RIGHT_BRACKET> )*
    <EQUAL>
    tokenValues = parseTokenValues()
    (<EOL> | <EOF>)
  )
  {
    return new PredicatedConfigValue(key.image, conditions.build(), tokenValues);
  }
}

void parseCondition(ImmutableSortedSet.Builder<Condition> conditions) :
{
  Token key;
  Token value = null;
  boolean isPrefix = false;
}
{
  (
    key = <CONDITION_IDENTIFIER>
    <CONDITION_EQUAL>
    [ value = <CONDITION_IDENTIFIER> ]
    [ <STAR> { isPrefix = true; } ]
  )
  {
    conditions.add(new Condition(key.image, value == null ? "" : value.image, isPrefix));
  }
}

ImmutableList<TokenValue> parseTokenValues() :
{
  ImmutableList.Builder<TokenValue> valueTokens = ImmutableList.builder();
  TokenValue tokenValue;
  Token token;
}
{
  (
    ( token = <VALUE_LITERAL> | token = <RIGHT_PAREN> | token = <RIGHT_BRACE> )
    { valueTokens.add(TokenValue.literal(token.image)); }
  |
    tokenValue = parseInterpolation()
    { valueTokens.add(tokenValue); }
  )*
  {
    return valueTokens.build();
  }
}

TokenValue parseInterpolation() :
{
  TokenValue result;
  Token token;
}
{
  (
    <DOLLAR_LEFT_BRACE>
    result = parseInterpolationBody()
    <RIGHT_BRACE>
  |
    <DOLLAR_LEFT_PAREN>
    result = parseInterpolationBody()
    <RIGHT_PAREN>
  |
    token = <DOLLAR_LITERAL>
    { result = TokenValue.interpolation(token.image.substring(1)); }
  )
  {
    return result;
  }
}

TokenValue parseInterpolationBody() :
{
    ImmutableList.Builder<TokenValue> tokenValues = ImmutableList.builder();
    Token token;
    TokenValue interpolation;
}
{
  (
    token = <VALUE_LITERAL>
    { tokenValues.add(TokenValue.literal(token.image)); }
  |
    interpolation = parseInterpolation()
    { tokenValues.add(interpolation); }
  )*
  {
    return TokenValue.interpolation(tokenValues.build());
  }
}
